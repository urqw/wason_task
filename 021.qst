/*12345678901234567890123456789012345678901234567890123456789012345678901234567890*/

:b00b00
  pln Задача Уэйсона
  pln
  pln
  btn b00200, Начать
  btn b00120, Об игре
end

:b00120
  pln Об игре:
  pln
  pln Вольная модификация задачи Уэйсона демонстрирует такой подход к моделированию мира, когда верный вариант невозможно угадать, если выбор был сделан неверно. 
  pln
  pln Написана под urqw, на ЗОК-2019 "Умчи меня, Олень!",
  pln название: "Задача Уэйсона",
  pln написал: qwerty, с благодарностью Акеле и Эноле за помощь.
  pln
  pln
  btn b00200, Начать
  btn b00b00, Заново
end

:b00200
  pln Ты сидишь на длинной деревянной скамейке за длинным деревянным столом. Напротив тебя на такой же скамейке сидит [[человек|b00400]] в чёрном балахоне. На столе находится [[свеча|b00300]] и [[колода|b00500]] тех самых карт.
end

:b00300
  pln Свеча толстая, наполовину сгоревшая, в простой металлической чашке.
  pln 
  goto b00200
end

:b00400
  pln Человек смотрит не на тебя. Взор его опущен, будто он разглядывает стол. Его ладони лежат на столе, из-под одной из них выглядывает кусок картона.
  pln
  goto b00200
end

:b00500
  pln Все карты в колоде двусторонние, причём с одной стороны каждой карты изображена какая-то цифра, а с другой стороны - буква.
  pln
  btn b01100, Стукнуть по столу, подавая знак готовности
  goto b00200

end

:b01100
  п_масс_0 = "?"
  п_масс_1 = "A"
  п_масс_2 = "E"
  п_масс_3 = "У"
  п_масс_4 = "B"
  п_масс_5 = "C"
  п_масс_6 = "K"
  п_масс_7 = "4"
  п_масс_8 = "2"
  п_масс_9 = "6"
  п_масс_10 = "7"
  п_масс_11 = "5"
  п_масс_12 = "3"

  п_масс_20 = "гласная буква"
  п_масс_21 = "согласная буква"
  п_масс_22 = "чётное число"
  п_масс_23 = "нечётное число"
   

  п_буквы = rnd2-1
  /* 0 - не поменялось, 1 - поменялось */

  п_гласн = rnd2-1
  /* 0 - не поменялось, 1 - поменялось */

  п_чётн = rnd2-1
  /* 0 - не поменялось, 1 - поменялось */

  instr фраз_1 = #%п_масс_#(п_буквы*2 + п_гласн + 20)$$
  instr фраз_2 = #%п_масс_#((1-п_буквы)*2 + п_чётн + 20)$$
  
  instr п_карт_1а = #%п_масс_#(п_буквы*6 + п_гласн*3 + rnd3)$$
  instr п_карт_1б = #%п_масс_0$
  карта_1_перевёрнута = 0

  instr п_карт_2а = #%п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$$
  instr п_карт_2б = #%п_масс_0$
  карта_2_перевёрнута = 0

  instr п_карт_3а = #%п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$$
  instr п_карт_3б = #%п_масс_0$
  карта_3_перевёрнута = 0

  instr п_карт_4а = #%п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + rnd3)$$
  instr п_карт_4б = #%п_масс_0$
  карта_4_перевёрнута = 0

  /* определяем порядок вывода карт */
  /* какую карту выводим первой: */
  card_1 = rnd4

  card_2 = rnd3
  if card_2 = card_1 then card_2 = 4

  card_3 = rnd2
  if (card_3 = card_2)or(card_3 = card_1) then card_3 = 3
  if (card_3 = card_2)or(card_3 = card_1) then card_3 = 4

  card_4 = 1
  if (card_4 = card_3)or(card_4 = card_2)or(card_4 = card_1) then card_4 = 2
  if (card_4 = card_3)or(card_4 = card_2)or(card_4 = card_1) then card_4 = 3
  if (card_4 = card_3)or(card_4 = card_2)or(card_4 = card_1) then card_4 = 4

  goto b01200
end

:b01200
  pln Ты легонько стукнул ладонью по столу. Человек отложил свой кусок картона, тщательно перемешал карты, спрятал колоду под стол и стал вытаскивать по одной карте. Каждую карту он осматривал, не поднимая выше уровня стола, а затем либо замешивал, либо клал одной из сторон на стол, пока не выложил [[четыре карты|b01300]]. После чего положил на стол оставшуюся колоду. Его [[прямоугольная картонка|b01400]] тоже лежит на столе.
  pln  
  btn b02100, Приступить
end

:b01300
  pln На столе лежат четыре карты: #%п_карт_#card_1$а$, #%п_карт_#card_2$а$, #%п_карт_#card_3$а$, #%п_карт_#card_4$а$. Человек ждёт. Его [[прямоугольная картонка|b01400]] выложена на стол так, что тебе хорошо её видно.
  pln  
  btn b02100, Приступить
end

:b01400
  pln На картонке написано: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?". Человек ждёт. Перед тобой выложены [[четыре карты|b01300]], к которым относится вопрос.
  pln  
  btn b02100, Приступить
end

:b02100
  pln Ты помнишь, что с одной стороны каждой карты - буква, а с другой - цифра.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?".
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  pln 1. первая = #%п_карт_#card_1$а$ 
  pln 2. вторая = #%п_карт_#card_2$а$ 
  pln 3. третья = #%п_карт_#card_3$а$ 
  pln 4. четыре = #%п_карт_#card_4$а$ 
  pln
  pln Ты можешь перевернуть две карты, прежде чем ответить на вопрос:
  pln
  btn b03100, 1 и 2 = перевернуть первую и вторую карты
  btn b03200, 1 и 3 = перевернуть первую и третью карты
  btn b03300, 1 и 4 = перевернуть первую и четвёртую карты
  btn b03400, 2 и 3 = перевернуть вторую и третью карты
  btn b03500, 2 и 4 = перевернуть вторую и четвёртую карты
  btn b03600, 3 и 4 = перевернуть третью и четвёртую карты
end

:проц_1_предв_ош
  п_карт_1б = п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$
end
:проц_2_предв_ош
  k = rnd2
  п_карт_2б = п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + rnd3)$
  if k=2 then п_карт_2б = п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$
end
:проц_3_предв_ош
  k = rnd2
  п_карт_3б = п_масс_#(п_буквы*6 + п_гласн*3 + rnd3)$
  if k=2 then п_карт_3б = п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$
end
:проц_4_предв_ош
  п_карт_4б = п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$
end

:b03100
  карта_1_перевёрнута = 1
  карта_2_перевёрнута = 1

  if (card_1 = 1)and(card_2 = 4) then goto b08100
  if (card_1 = 4)and(card_2 = 1) then goto b08100
  
  if (card_1 = 1)or(card_2 = 1) then proc проц_1_предв_ош
  if (card_1 = 2)or(card_2 = 2) then proc проц_2_предв_ош
  if (card_1 = 3)or(card_2 = 3) then proc проц_3_предв_ош
  if (card_1 = 4)or(card_2 = 4) then proc проц_4_предв_ош

  goto b04100   
end
:b03200
  карта_1_перевёрнута = 1
  карта_3_перевёрнута = 1

  if (card_1 = 1)and(card_3 = 4) then goto b08100
  if (card_1 = 4)and(card_3 = 1) then goto b08100
  
  if (card_1 = 1)or(card_3 = 1) then proc проц_1_предв_ош
  if (card_1 = 2)or(card_3 = 2) then proc проц_2_предв_ош
  if (card_1 = 3)or(card_3 = 3) then proc проц_3_предв_ош
  if (card_1 = 4)or(card_3 = 4) then proc проц_4_предв_ош

  goto b04100   
end
:b03300
  карта_1_перевёрнута = 1
  карта_4_перевёрнута = 1

  if (card_1 = 1)and(card_4 = 4) then goto b08100
  if (card_1 = 4)and(card_4 = 1) then goto b08100
  
  if (card_1 = 1)or(card_4 = 1) then proc проц_1_предв_ош
  if (card_1 = 2)or(card_4 = 2) then proc проц_2_предв_ош
  if (card_1 = 3)or(card_4 = 3) then proc проц_3_предв_ош
  if (card_1 = 4)or(card_4 = 4) then proc проц_4_предв_ош

  goto b04100   
end
:b03400
  карта_2_перевёрнута = 1
  карта_3_перевёрнута = 1

  if (card_2 = 1)and(card_3 = 4) then goto b08100
  if (card_2 = 4)and(card_3 = 1) then goto b08100
  
  if (card_2 = 1)or(card_3 = 1) then proc проц_1_предв_ош
  if (card_2 = 2)or(card_3 = 2) then proc проц_2_предв_ош
  if (card_2 = 3)or(card_3 = 3) then proc проц_3_предв_ош
  if (card_2 = 4)or(card_3 = 4) then proc проц_4_предв_ош

  goto b04100   
end
:b03500
  карта_2_перевёрнута = 1
  карта_4_перевёрнута = 1

  if (card_2 = 1)and(card_4 = 4) then goto b08100
  if (card_2 = 4)and(card_4 = 1) then goto b08100
  
  if (card_2 = 1)or(card_4 = 1) then proc проц_1_предв_ош
  if (card_2 = 2)or(card_4 = 2) then proc проц_2_предв_ош
  if (card_2 = 3)or(card_4 = 3) then proc проц_3_предв_ош
  if (card_2 = 4)or(card_4 = 4) then proc проц_4_предв_ош

  goto b04100   
end
:b03600
  карта_3_перевёрнута = 1
  карта_4_перевёрнута = 1

  if (card_3 = 1)and(card_4 = 4) then goto b08100
  if (card_3 = 4)and(card_4 = 1) then goto b08100
  
  if (card_3 = 1)or(card_4 = 1) then proc проц_1_предв_ош
  if (card_3 = 2)or(card_4 = 2) then proc проц_2_предв_ош
  if (card_3 = 3)or(card_4 = 3) then proc проц_3_предв_ош
  if (card_3 = 4)or(card_4 = 4) then proc проц_4_предв_ош

  goto b04100   
end

:b04100
  pln Ты помнишь, что с одной стороны каждой карты - буква, а с другой - цифра.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?".
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  if (карта_1_перевёрнута = 0) then pln 1. первая = #%п_карт_#card_1$а$, оборотная сторона неизвестна
  if (карта_1_перевёрнута = 1) then pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  if (карта_2_перевёрнута = 0) then pln 2. вторая = #%п_карт_#card_2$а$, оборотная сторона неизвестна
  if (карта_2_перевёрнута = 1) then pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  if (карта_3_перевёрнута = 0) then pln 3. третья = #%п_карт_#card_3$а$, оборотная сторона неизвестна
  if (карта_3_перевёрнута = 1) then pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  if (карта_4_перевёрнута = 0) then pln 4. четыре = #%п_карт_#card_4$а$, оборотная сторона неизвестна
  if (карта_4_перевёрнута = 1) then pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  pln Каким будет твой ответ?
  pln
  btn b04200, Да, утверждение верно
  btn b04600, Нет, утверждение не верно
end

:проц_1_фин_ош_да
  п_карт_1б = п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + rnd3)$
end
:проц_4_фин_ош_да
  п_карт_4б = п_масс_#(п_буквы*6 + п_гласн*3 + rnd3)$
end

:b04200
  if (карта_1_перевёрнута = 0)and(card_1=4) then finum = 4 && proc проц_4_фин_ош_да
  if (карта_2_перевёрнута = 0)and(card_2=4) then finum = 4 && proc проц_4_фин_ош_да
  if (карта_3_перевёрнута = 0)and(card_3=4) then finum = 4 && proc проц_4_фин_ош_да
  if (карта_4_перевёрнута = 0)and(card_4=4) then finum = 4 && proc проц_4_фин_ош_да

  if (карта_1_перевёрнута = 0)and(card_1=1) then finum = 1 && proc проц_1_фин_ош_да
  if (карта_2_перевёрнута = 0)and(card_2=1) then finum = 1 && proc проц_1_фин_ош_да
  if (карта_3_перевёрнута = 0)and(card_3=1) then finum = 1 && proc проц_1_фин_ош_да
  if (карта_4_перевёрнута = 0)and(card_4=1) then finum = 1 && proc проц_1_фин_ош_да

  if (карта_1_перевёрнута = 0)and(card_1=2) then proc проц_2_предв_ош
  if (карта_2_перевёрнута = 0)and(card_2=2) then proc проц_2_предв_ош
  if (карта_3_перевёрнута = 0)and(card_3=2) then proc проц_2_предв_ош
  if (карта_4_перевёрнута = 0)and(card_4=2) then proc проц_2_предв_ош

  if (карта_1_перевёрнута = 0)and(card_1=3) then proc проц_3_предв_ош
  if (карта_2_перевёрнута = 0)and(card_2=3) then proc проц_3_предв_ош
  if (карта_3_перевёрнута = 0)and(card_3=3) then proc проц_3_предв_ош
  if (карта_4_перевёрнута = 0)and(card_4=3) then proc проц_3_предв_ош

  if finum = 1 then goto b04300
  if finum = 4 then goto b04400
  
  pln
  pln *** Упс! Что-то пошло не так (код#b04200) ***
  pln
end

:b04300
  /*pln :b04300 #card_1$, #card_2$, #card_3$, #card_4$*/
  pln Человек переворачивает оставшиеся карты.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?". Твой ответ: "Да, утверждение верно."
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  if card_1 = 1 then pln С одной стороны первой карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  if card_2 = 1 then pln С одной стороны второй карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  if card_3 = 1 then pln С одной стороны третьей карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  if card_4 = 1 then pln С одной стороны четвёртой карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  pln
  btn b10100, Финал
end

:b04400
  /*pln :b04400 #card_1$, #card_2$, #card_3$, #card_4$*/
  pln Человек переворачивает оставшиеся карты.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне- #%фраз_2$?". Твой ответ: "Да, утверждение верно."
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  if card_1 = 4 then pln С одной стороны первой карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  if card_2 = 4 then pln С одной стороны второй карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  if card_3 = 4 then pln С одной стороны третьей карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  if card_4 = 4 then pln С одной стороны четвёртой карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  pln
  btn b10100, Финал
end

:проц_1_фин_ош_нет
  п_карт_1б = п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$
end
:проц_4_фин_ош_нет
  п_карт_4б = п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$
end

:b04600
  if (карта_1_перевёрнута = 0)and(card_1=4) then finum = 4 && proc проц_4_фин_ош_нет
  if (карта_2_перевёрнута = 0)and(card_2=4) then finum = 4 && proc проц_4_фин_ош_нет
  if (карта_3_перевёрнута = 0)and(card_3=4) then finum = 4 && proc проц_4_фин_ош_нет
  if (карта_4_перевёрнута = 0)and(card_4=4) then finum = 4 && proc проц_4_фин_ош_нет

  if (карта_1_перевёрнута = 0)and(card_1=1) then finum = 1 && proc проц_1_фин_ош_нет
  if (карта_2_перевёрнута = 0)and(card_2=1) then finum = 1 && proc проц_1_фин_ош_нет
  if (карта_3_перевёрнута = 0)and(card_3=1) then finum = 1 && proc проц_1_фин_ош_нет
  if (карта_4_перевёрнута = 0)and(card_4=1) then finum = 1 && proc проц_1_фин_ош_нет

  if (карта_1_перевёрнута = 0)and(card_1=2) then proc проц_2_предв_ош
  if (карта_2_перевёрнута = 0)and(card_2=2) then proc проц_2_предв_ош
  if (карта_3_перевёрнута = 0)and(card_3=2) then proc проц_2_предв_ош
  if (карта_4_перевёрнута = 0)and(card_4=2) then proc проц_2_предв_ош

  if (карта_1_перевёрнута = 0)and(card_1=3) then proc проц_3_предв_ош
  if (карта_2_перевёрнута = 0)and(card_2=3) then proc проц_3_предв_ош
  if (карта_3_перевёрнута = 0)and(card_3=3) then proc проц_3_предв_ош
  if (карта_4_перевёрнута = 0)and(card_4=3) then proc проц_3_предв_ош

  if finum = 1 then goto b04700
  if finum = 4 then goto b04700
  
  pln
  pln *** Упс! Что-то пошло не так (код#b04600) ***
  pln
end

:b04700
  pln Человек переворачивает оставшиеся карты.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?". Твой ответ: "Нет, утверждение не верно."
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  pln Как видишь, везде, где есть #%фраз_1$, с другой стороны этой карты всегда #%фраз_2$. Ты ошибся.
  pln
  btn b10100, Финал
end

:b08100
  k = rnd3
  п_карт_1б = п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$
  if k=3 then п_карт_1б = п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + rnd3)$

  k = rnd3
  п_карт_4б = п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$
  if k=3 then п_карт_4б = п_масс_#(п_буквы*6 + п_гласн*3 + rnd3)$
  goto b08200
end

:b08200
  pln Ты помнишь, что с одной стороны каждой карты - буква, а с другой - цифра.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?".
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  if (карта_1_перевёрнута = 0) then pln 1. первая = #%п_карт_#card_1$а$, оборотная сторона неизвестна
  if (карта_1_перевёрнута = 1) then pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  if (карта_2_перевёрнута = 0) then pln 2. вторая = #%п_карт_#card_2$а$, оборотная сторона неизвестна
  if (карта_2_перевёрнута = 1) then pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  if (карта_3_перевёрнута = 0) then pln 3. третья = #%п_карт_#card_3$а$, оборотная сторона неизвестна
  if (карта_3_перевёрнута = 1) then pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  if (карта_4_перевёрнута = 0) then pln 4. четыре = #%п_карт_#card_4$а$, оборотная сторона неизвестна
  if (карта_4_перевёрнута = 1) then pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  pln Каким будет твой ответ?
  pln
  btn b08300, Да, утверждение верно
  btn b08700, Нет, утверждение не верно
end

:b08300
  k = rnd2
  п_карт_2б = п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$
  if k=2 then п_карт_2б = п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + rnd3)$
  k = rnd2
  п_карт_3б = п_масс_#(п_буквы*6 + п_гласн*3 + rnd3)$
  if k=2 then п_карт_3б = п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$

  flag = 0
  if п_карт_4б = (п_масс_#(п_буквы*6 + п_гласн*3 + 1)$) then flag = 4
  if п_карт_4б = (п_масс_#(п_буквы*6 + п_гласн*3 + 2)$) then flag = 4
  if п_карт_4б = (п_масс_#(п_буквы*6 + п_гласн*3 + 3)$) then flag = 4

  if п_карт_1б = (п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + 1)$) then flag = 1
  if п_карт_1б = (п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + 2)$) then flag = 1
  if п_карт_1б = (п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + 3)$) then flag = 1

  pln Человек переворачивает оставшиеся карты.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?". Твой ответ: "Да, утверждение верно."
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  if flag = 1 then goto b08400
  if flag = 4 then goto b08500
  if flag = 0 then goto b08600
  pln *** Упс! Что-то пошло не так (код#b08300) ***
end

:b08400
  /***/
  if card_1 = 1 then pln С одной стороны первой карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  if card_2 = 1 then pln С одной стороны второй карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  if card_3 = 1 then pln С одной стороны третьей карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  if card_4 = 1 then pln С одной стороны четвёртой карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты ошибся.
  pln
  btn b10100, Финал
end

:b08500
  /***/
  if card_1 = 4 then pln С одной стороны первой карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  if card_2 = 4 then pln С одной стороны второй карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  if card_3 = 4 then pln С одной стороны третьей карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  if card_4 = 4 then pln С одной стороны четвёртой карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты ошибся.
  pln
  btn b10100, Финал
end

:b08600
  /***/
  pln Как видишь, везде, где есть #%фраз_1$, с другой стороны этой карты всегда #%фраз_2$. Ты прав!
  pln
  btn b10200, Финал
end

:b08700
  k = rnd2
  п_карт_2б = п_масс_#((1-п_буквы)*6 + п_чётн*3 + rnd3)$
  if k=2 then п_карт_2б = п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + rnd3)$
  k = rnd2
  п_карт_3б = п_масс_#(п_буквы*6 + п_гласн*3 + rnd3)$
  if k=2 then п_карт_3б = п_масс_#(п_буквы*6 + (1-п_гласн)*3 + rnd3)$

  flag = 0
  if п_карт_4б = (п_масс_#(п_буквы*6 + п_гласн*3 + 1)$) then flag = 4
  if п_карт_4б = (п_масс_#(п_буквы*6 + п_гласн*3 + 2)$) then flag = 4
  if п_карт_4б = (п_масс_#(п_буквы*6 + п_гласн*3 + 3)$) then flag = 4

  if п_карт_1б = (п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + 1)$) then flag = 1
  if п_карт_1б = (п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + 2)$) then flag = 1
  if п_карт_1б = (п_масс_#((1-п_буквы)*6 + (1-п_чётн)*3 + 3)$) then flag = 1

  pln Человек переворачивает оставшиеся карты.
  pln
  pln На дальней стороне стола лежит картонка с надписью: "Верно ли, что если на одной стороне карты #%фраз_1$, то на другой её стороне - #%фраз_2$?". Твой ответ: "Нет, утверждение не верно."
  pln
  pln Ближе к тебе лежат четыре карты:
  pln
  pln 1. первая = #%п_карт_#card_1$б$, оборотная сторона = #%п_карт_#card_1$а$
  pln 2. вторая = #%п_карт_#card_2$б$, оборотная сторона = #%п_карт_#card_2$а$
  pln 3. третья = #%п_карт_#card_3$б$, оборотная сторона = #%п_карт_#card_3$а$
  pln 4. четыре = #%п_карт_#card_4$б$, оборотная сторона = #%п_карт_#card_4$а$
  pln
  if flag = 1 then goto b08800
  if flag = 4 then goto b08900
  if flag = 0 then goto b08950 
  pln *** Упс! Что-то пошло не так (код#b08700) ***
end

:b08800
  /***/
  if card_1 = 1 then pln С одной стороны первой карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты прав!
  if card_2 = 1 then pln С одной стороны второй карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты прав!
  if card_3 = 1 then pln С одной стороны третьей карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты прав!
  if card_4 = 1 then pln С одной стороны четвёртой карты - "#%п_карт_1а$", но на другой её стороне - "#%п_карт_1б$". Очевидно, ты прав!
  pln
  btn b10200, Финал
end

:b08900
  /***/
  if card_1 = 4 then pln С одной стороны первой карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты прав!
  if card_2 = 4 then pln С одной стороны второй карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты прав!
  if card_3 = 4 then pln С одной стороны третьей карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты прав!
  if card_4 = 4 then pln С одной стороны четвёртой карты - "#%п_карт_4б$", но на другой её стороне - "#%п_карт_4а$". Очевидно, ты прав!
  pln
  btn b10200, Финал
end

:b08950
  /***/
  pln Как видишь, везде, где есть #%фраз_1$, с другой стороны этой карты всегда #%фраз_2$. Ты ошибся.
  pln
  btn b10100, Финал
end

:b10100
  pln Вы с человеком в балахоне вышли на заснеженную улицу, освещаемую луной и звёздами.
  pln
  pln  - Умчи меня, олень! - произнёс он волшебные слова.
  pln
  pln Ты стал на четвереньки и превратился в оленя. Человек ловко вскочил и помчался верхом по снежным просторам.
  pln
  pln
  pln КОНЕЦ
  pln
  pln 
  btn b00b00, Не будь оленем, реши эту задачу!
end

:b10200
  pln Вы с человеком в балахоне вышли на заснеженную улицу, освещаемую луной и звёздами.
  pln
  pln  - Умчи меня, олень! - произнёс ты волшебные слова.
  pln
  pln Человек в балахоне стал на четвереньки и превратился в оленя. Ты ловко вскочил и помчался верхом по снежным просторам.
  pln
  pln
  pln КОНЕЦ
  pln
  pln 
  btn b00b00, Молодец! Может, попробуешь ещё раз?
end

